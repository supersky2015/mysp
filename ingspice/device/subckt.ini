
[led]
.SUBCKT LED  K A
D1 1 A  LED_mod
V  K  1  DC 0
.MODEL LED_mod D (
+ IS=1.112e-021 N=3.007e+000 RS=3.513e-003
+ BV=6.100e+001 IBV=6.000e-003
+ EG=1.110e+000 XTI=3.000e+000 
+ CJO=3.238e-011, M=3.388e-001 VJ=3.250e-001
+ FC=5.000e-001 KF=0.000e+000 AF=1.000e+000 )
.ENDS

[spst]
.subckt spst 1 2 Params: VStatus=0 Ron=1e-8 Roff=1e30
R1 0 6 20 
V1 6 0 DC {Vstatus}
W1 1 2 V1 NO_contact 
.MODEL NO_contact csw (IT=0.05 IH=0.025 RON={Ron} ROFF={Roff})
.ENDS

[spdt]
.subckt spdt 1 2 3 Params: VStatus=0 Ron=1e-8 Roff=1e30
R1 0 6 20
V1 6 0 DC {Vstatus}
W0 2 1 V1 NC_contact 
W1 2 3 V1 NO_contact
.MODEL NO_contact csw (IT=0.05 IH=0.025 RON={Ron} ROFF={Roff})
.MODEL NC_contact csw (IT=0.05 IH=0.025 RON={Roff} ROFF={Ron})
.ENDS


#mixed
[LMC555_TIMER]
.subckt LMC555_TIMER 0 2 3 4 5 6 7 8 
rn1 8 5 5k
rn2 5 51 5k
rn3 51 0 5k
aop1 %vd(5 6) 56 op
aop2 %vd(2 51) 52 op
.model op limit (gain= 3000,      
+ out_upper_limit=5, 
+ out_lower_limit=-5, 
+ limit_range=1 fraction=true)
aadc1 [56 52] [r s] ADC1
.MODEL ADC1 adc_bridge (in_low= 2.5  in_high = 2.5 rise_delay= 1e-12 fall_delay= 1e-12)
anand1 [r Q2] Q1  nand1
anand2 [s Q1] Q2  nand1
.model nand1 d_nand(rise_delay=1n)
adac1 [q1 q2] [66 62] DAC1
rad3 66 0 1
rad4 62 0 1
aadc4 [4] [40] ADC1
ainv2 40 41 inv1
adlatch q1 2u 41 3d Qb Qc dlt
.model dlt d_dlatch(rise_delay=1e-12)
apu1 2u pullup1
.model pullup1 d_pullup(load=10e-12)
apd1 3d pulldown1 
.model pulldown1 d_pulldown(load=10e-12)
ainv1 Qb 31 inv1
.model inv1 d_inverter(rise_delay=1e-12)
adac72 [Qb] [72] DAC1   
adac31 [31] [32] DAC1
r30 32 0 1g
b1 333 0 v=(v(32)*v(8)/5)
r3 333 0 1g
aslew %vd(333 0) %vd(3 0) Slew_Rate_Block
.MODEL Slew_Rate_Block slew(
+ rise_slope=66.666e+6
+ fall_slope=66.666e+6)
.MODEL DAC1 dac_bridge  (out_low= 0.0 out_high= 5.0 out_undef=0.5) 
rad5 72 0 1meg
mdis 7 72 0 0 mdis 
.MODEL mdis nmos (VTO=1.29 PHI=0.4 LAMBDA=5 KP=4.3m LD=12.8u GAMMA =3)
.ends

[LM555_TIMER]
.subckt LM555_TIMER 0 2 3 4 5 6 7 8
rn1 8 5 5k
rn2 5 51 5k
rn3 51 0 5k
aop1 %vd(5 6) 56 op
aop2 %vd(2 51) 52 op
.model op limit (gain= 3000,      
+ out_upper_limit=5, 
+ out_lower_limit=-5, 
+ limit_range=1 fraction=true)
aadc1 [56 52] [r s] ADC1
.MODEL ADC1 adc_bridge (in_low= 2.5  in_high = 2.5 rise_delay= 1e-12 fall_delay= 1e-12) 
anand1 [r Q2] Q1  nand1
anand2 [s Q1] Q2  nand1
.model nand1 d_nand(rise_delay=1n)
adac1 [q1 q2] [66 62] DAC1
rad3 66 0 1
rad4 62 0 1
aadc4 [4] [40] ADC1
ainv2 40 41 inv1
adlatch q1 2u 41 3d Qb Qc dlt
.model dlt d_dlatch(rise_delay=1e-12)
apu1 2u pullup1
.model pullup1 d_pullup(load=10e-12)
apd1 3d pulldown1 
.model pulldown1 d_pulldown(load=10e-12)
ainv1 Qb 31 inv1
.model inv1 d_inverter(rise_delay=1e-12)
adac72 [Qb] [72] DAC1   
adac31 [31] [32] DAC1
r30 32 0 1g
b1 333 0 v=(v(32)*v(8)/5)
r3 333 0 1g
aslew %vd(333 0) %vd(3 0) Slew_Rate_Block
.MODEL Slew_Rate_Block slew(
+ rise_slope=10e+6
+ fall_slope=10e+6)
.MODEL DAC1 dac_bridge  (out_low= 0.0 out_high= 5.0 out_undef=0.5) 
rad5 72 0 1meg
rdisb 71 72 1 
qdis 7 71 0 qdis 
.MODEL qdis npn ()
.ends

[seven_seg_com_neg]
.subckt  seven_seg_com_neg 1 2 3 4 5 6 7 10
V1 1 1a DC 0
V2 2 2a DC 0
V3 3 3a DC 0
V4 4 4a DC 0
V5 5 5a DC 0
V6 6 6a DC 0
V7 7 7a DC 0
D1 1a 10 DLEDSEG
D2 2a 10 DLEDSEG
D3 3a 10 DLEDSEG
D4 4a 10 DLEDSEG
D5 5a 10 DLEDSEG
D6 6a 10 DLEDSEG
D7 7a 10 DLEDSEG
.MODEL DLEDSEG  D (
+ IS=1.024e-017 N=2.005e+000 RS=2.342e-003
+ BV=6.100e+001 IBV=6.000e-006
+ EG=1.110e+000 XTI=3.000e+000 
+ CJO=3.238e-011, M=3.388e-001 VJ=3.250e-001
+ FC=5.000e-001 KF=0.000e+000 AF=1.000e+000 )
.ends

[seven_seg_com_pos]
.subckt  seven_seg_com_pos 1 2 3 4 5 6 7 10
V1 1a 1 DC 0
V2 2a 2 DC 0
V3 3a 3 DC 0
V4 4a 4 DC 0
V5 5a 5 DC 0
V6 6a 6 DC 0
V7 7a 7 DC 0
D1 10 1a DLEDSEG
D2 10 2a DLEDSEG
D3 10 3a DLEDSEG
D4 10 4a DLEDSEG
D5 10 5a DLEDSEG
D6 10 6a DLEDSEG
D7 10 7a DLEDSEG
.MODEL DLEDSEG  D (
+ IS=1.024e-017 N=2.005e+000 RS=2.342e-003
+ BV=6.100e+001 IBV=6.000e-006
+ EG=1.110e+000 XTI=3.000e+000 
+ CJO=3.238e-011, M=3.388e-001 VJ=3.250e-001
+ FC=5.000e-001 KF=0.000e+000 AF=1.000e+000 )
.ends


[cccs]
.SUBCKT cccs 1 2 3 4 Params: value=1.0 
v1 2 1 dc 0
f1 4 3 v1 {value}
.ends

[ccvs]
.SUBCKT ccvs 1 2 3 4 Params: value=1.0 
v1 2 1 dc 0
h1 4 3 v1 {value}
.ends